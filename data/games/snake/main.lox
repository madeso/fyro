// Based on "Coding "Snake" in 4 min 30 sec (plain browser JavaScript)" by Chris DeLeon
// https://www.youtube.com/watch?v=xGmXxpIj6vs
// refactored to be a bit less hacky and fit with fyro

class Point
{
	public var x;
	public var y;

	public fun init(x, y)
	{
		this.x = x;
		this.y = y;
	}
}

class GameState
{
	// player x y
	public var px = 10;
	public var py = 10;

	// input
	public var xv = 0;
	public var yv = 0;

	// game scale
	public var gs = 20;

	// tile count
	public var tc = 20;

	// apple x y
	public var ax = 15;
	public var ay = 15;

	public var trail = [];
	public var tail = 5;

	public var accum = 0.0;

	public var random = new fyro.Random();

	public fun update(dt)
	{
		var upper = 0.8;
		var time = 0.3;

		// handle input
		var input = fyro.get_input();
		if(input.current.axis_left_x < -upper) { this.xv = -1;  this.yv =  0; }
		if(input.current.axis_left_y < -upper) { this.xv =  0;  this.yv = -1; }
		if(input.current.axis_left_x >  upper) { this.xv =  1;  this.yv =  0; }
		if(input.current.axis_left_y >  upper) { this.xv =  0;  this.yv =  1; }

		this.accum += dt;
		if( this.accum < time) { return; }
		this.accum -= time;

		this.px += this.xv;
		this.py += this.yv;

		// wrap around
		if(this.px < 0) { this.px= this.tc-1; }
		if(this.px > this.tc-1) { this.px = 0; }
		if(this.py < 0) { this.py= this.tc-1; }
		if(this.py > this.tc-1) { this.py = 0; }

		// collision with tail
		for(var i=0; i<this.trail.len(); i+= 1)
		{
			if(this.trail[i].x == this.px and this.trail[i].y == this.py)
			{
				this.tail = 5;
				this.trail = [];
			}
		}

		// move tail
		this.trail.push(new Point(this.px, this.py));
		while(this.trail.len() > this.tail)
		{
			this.trail.remove_front();
		}

		// eat apple
		if(this.ax == this.px and this.ay == this.py)
		{
			this.tail+=1;
			this.ax = this.random.next_int(this.tc);
			this.ay = this.random.next_int(this.tc);
			input.run_haptics();
		}
	}

	public fun render(cmd)
	{
		// cmd.extend(200, 200);
		cmd.windowbox(this.gs * this.tc, this.gs * this.tc);

		// todo(Gustav): find better colors
		var background = new fyro.Rgb(0, 0, 0);
		var snake = new fyro.Rgb(0.75, 1, 0);
		var apple = new fyro.Rgb(1, 0, 0);

		cmd.clear(background);
	
		for(var i=0; i < this.trail.len(); i+=1)
		{
			cmd.rect(snake, this.trail[i].x*this.gs, this.trail[i].y*this.gs, this.gs-2, this.gs-2);
		}
		
		cmd.rect(apple, this.ax * this.gs, this.ay * this.gs, this.gs-2, this.gs-2);
	}
}

fyro.set_state(new GameState());
